package com.innstack.point
  
import com.innstack.point.User;
  
rule birthdayPoint22
    // 过生日，则加10分，并且将当月交易比数翻倍后再计算积分  
    salience 100  
    lock-on-active true  
    when  
        $user : User(birthDay == true)
    then  
        $user.setPoint($user.getPoint()+10);
        $user.setBuyNums($user.getBuyNums()*2);
        $user.setBuyMoney($user.getBuyMoney()*2);
        $user.setBillThisMonth($user.getBillThisMonth()*2);
          
        $user.recordPointLog($user.getUserName(),"birthdayPoint");
end  
  
rule billThisMonthPoint  
    // 2011-01-08 - 2011-08-08每月信用卡还款3次以上，每满3笔赠送30分  
    salience 99  
    lock-on-active true  
    date-effective "2011-01-08 23:59:59"
    date-expires "2011-08-08 23:59:59"
    when
        $user : User(billThisMonth >= 3)
    then  
        $user.setPoint($user.getPoint()+$user.getBillThisMonth()/3*30);
        $user.recordPointLog($user.getUserName(),"billThisMonthPoint");
end  
  
rule buyMoneyPoint  
    // 当月购物总金额100以上，每100元赠送10分  
    salience 98  
    lock-on-active true  
    when  
        $user : User(buyMoney >= 100)
    then  
        $user.setPoint($user.getPoint()+ (int)$user.getBuyMoney()/100 * 10);
        $user.recordPointLog($user.getUserName(),"buyMoneyPoint");
end  
  
rule buyNumsPoint  
    // 当月购物次数5次以上，每五次赠送50分  
    salience 97  
    lock-on-active true  
    when  
        $user : User(buyNums >= 5)
    then  
        $user.setPoint($user.getPoint()+$user.getBuyNums()/5 * 50);
        $user.recordPointLog($user.getUserName(),"buyNumsPoint");
end  
  
rule allFitPoint  
    // 特别的，如果全部满足了要求，则额外奖励100分  
    salience 96  
    lock-on-active true  
    when  
        $user:User(buyNums >= 5 && billThisMonth >= 3 && buyMoney >= 100)
    then  
        $user.setPoint($user.getPoint()+ 100);
        $user.recordPointLog($user.getUserName(),"allFitPoint");
end  